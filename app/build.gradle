plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

Properties properties = new Properties();
properties.load(new FileInputStream(file("signing.properties")))

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tool_version

    defaultConfig {
        applicationId "cn.com.ava.zqproject"
        minSdkVersion minimum_sdk_version
        targetSdkVersion target_sdk_version
        versionCode app_version
        versionName app_version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias properties['KEY_ALIAS']
            keyPassword properties['KEY_PASSWORD']
            storeFile file(properties['KEYSTORE_FILE'])
            storePassword properties['KEYSTORE_PASSWORD']
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding = true
    }

    applicationVariants.all{variant ->
        def date = new Date().format("yyyy-MM-ddHHmm", TimeZone.getTimeZone("GMT+08"))
        def buildSuffix = variant.buildType.name.equals('release') ? "release" : "debug"
        variant.outputs.all {
            if (buildSuffix.equals('release')) {
                outputFileName = "政企录播V${versionName}-${buildSuffix}-${date}.apk"
            }
        }
    }


}

dependencies {
    implementation project(path: ':common')
    implementation project(path: ':base')
    implementation project(path: ':lubosdkv2')
    implementation project(path: ':player')
    implementation project(path: ':authcode')
    implementation deps.xcrash
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.mcxtzhang:SwipeDelMenuLayout:V1.3.0'
    implementation files('libs/commons-codec-1.15.jar')
    generalImplementation.each {
        implementation "$it"
    }
    lifecycleLibs.each {
        api "$it"
    }
    debugImplementation deps.leakcanary
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    kapt deps.lifecycle_compiler
}